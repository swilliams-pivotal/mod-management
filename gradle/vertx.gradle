apply plugin: 'maven-publish'

ext.moduleName = "${project.name}".replaceFirst 'mod-', ''
ext.modulePackageName = "${project.group}~${project.moduleName}~${project.version}"

configurations {
  provided
  compile.extendsFrom provided
}

publishing {
  repositories {
    maven {
      name 'snapshots'
      url 'https://oss.sonatype.org/content/repositories/snapshots'
      credentials {
        username project.sonatypeUsername
        password project.sonatypePassword
      }
    }
    maven {
      name 'staging'
      url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
      credentials {
        username project.sonatypeUsername
        password project.sonatypePassword
      }
    }
  }
  publications {
    maven(MavenPublication) {
      mavenProjectIdentity.artifactId = project.moduleName
      artifact("build/libs/${project.moduleName}-${project.version}.zip") {
        extension "zip"
        builtBy zipMod
      }
    }
  }
}

jar.enabled = false

task makeMod(type: Copy) {
  destinationDir mkdir("build/mod/$modulePackageName")
  outputs.file file("build/mod/$modulePackageName")

  into "build/mod/$modulePackageName"
  from compileJava
  from compileGroovy
  from processResources

  into('lib') {
    from configurations.compile - configurations.provided
  }
}

task zipMod(type: Zip, dependsOn:['makeMod']) {
  destinationDir mkdir('build/libs')
  inputs.files file('build/mod')
  outputs.files destinationDir

  from "build/mod/$modulePackageName"

  archiveName = "${project.moduleName}-${project.version}.zip"
  // includes = ["$modulePackageName"]
}

artifacts {
  archives zipMod
}


